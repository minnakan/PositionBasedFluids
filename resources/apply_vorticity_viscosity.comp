#version 430 core

layout(local_size_x = 256) in;

struct Particle {
    vec3 position;
    float padding1;
    vec3 velocity;
    float padding2;
    vec3 predictedPos;
    float padding3;
    vec3 color;
    float padding4;
    float density;
    float lambda;
    vec2 padding5;
};

layout(std140, binding = 0) uniform SimulationParams {
    float dt;
    float _pad0;
    float _pad1;
    float _pad2;
    vec4 gravity;
    float particleRadius;
    float h;
    float _pad3;
    float _pad4;
    vec4 minBoundary;
    vec4 maxBoundary;
    uint numParticles;
    float cellSize;
    uint maxParticlesPerCell;
    float restDensity;
    float vorticityEpsilon;
    float xsphViscosityCoeff;
    float _pad5;
    float _pad6; 
};

layout(std430, binding = 1) buffer ParticleBuffer {
    Particle particles[];
};

layout(std430, binding = 2) buffer CellCounts {
    uint cellCounts[];
};

layout(std430, binding = 3) buffer CellParticles {
    uint cellParticles[];
};

uint getCellIndex(vec3 position) {
    ivec3 cellPos = ivec3(floor((position - minBoundary.xyz) / cellSize));
    ivec3 gridDim = ivec3(ceil((maxBoundary.xyz - minBoundary.xyz) / cellSize));
    cellPos = clamp(cellPos, ivec3(0), gridDim - ivec3(1));
    return uint(cellPos.x + cellPos.y * gridDim.x + cellPos.z * gridDim.x * gridDim.y);
}

vec3 gradW_Vorticity(vec3 r, float rlen, float h) {
    if(rlen > h || rlen < 0.0001) return vec3(0.0);
    float factor = -45.0/(3.14159 * pow(h, 6)) * pow(h - rlen, 2);
    return factor * normalize(r);
}

float W_Poly6(float rlen, float h) {
    if(rlen > h) return 0.0;
    float term = h*h - rlen*rlen;
    return 315.0f / (64.0f * 3.14159f * pow(h, 9)) * term * term * term;
}

void main() {
    uint id = gl_GlobalInvocationID.x;
    if (id >= numParticles) return;
    
    vec3 pos = particles[id].position;
    vec3 vel = particles[id].velocity;
    ivec3 cellPos = ivec3(floor((pos - minBoundary.xyz) / cellSize));
    ivec3 gridDim = ivec3(ceil((maxBoundary.xyz - minBoundary.xyz) / cellSize));
    
    //vorticity and XSPH
    vec3 vorticity = vec3(0.0);
    vec3 xsphVelocityChange = vec3(0.0);
    
    for (int x = -1; x <= 1; x++) {
        for (int y = -1; y <= 1; y++) {
            for (int z = -1; z <= 1; z++) {
                ivec3 neighborCellPos = cellPos + ivec3(x, y, z);
                
                if (any(lessThan(neighborCellPos, ivec3(0))) || 
                    any(greaterThanEqual(neighborCellPos, gridDim)))
                    continue;
                    
                uint neighborCellIndex = getCellIndex(pos + vec3(x,y,z) * cellSize);
                uint particlesInCell = cellCounts[neighborCellIndex];
                
                for (uint j = 0; j < particlesInCell && j < maxParticlesPerCell; j++) {
                    uint neighborId = cellParticles[neighborCellIndex * maxParticlesPerCell + j];
                    
                    if (neighborId == id) continue;
                    
                    vec3 neighborPos = particles[neighborId].position;
                    vec3 neighborVel = particles[neighborId].velocity;
                    
                    vec3 r = pos - neighborPos;
                    float rlen = length(r);
                    
                    if (rlen < h && rlen > 0.0001) {
                        vec3 velDiff = neighborVel - vel;
                        vec3 gradW = gradW_Vorticity(r, rlen, h);
                        
                        vorticity += cross(velDiff, gradW);
                        
                        float weight = W_Poly6(rlen, h);
                        xsphVelocityChange += velDiff * weight;
                    }
                }
            }
        }
    }
    
    // Calculate vorticity confinement force
    vec3 vorticityForce = vec3(0.0);
    float vorticityMagnitude = length(vorticity);
    
    if (vorticityMagnitude > 0.0001) {
        // Calculate eta (gradient of vorticity magnitude)
        vec3 eta = vec3(0.0);
        
        for (int x = -1; x <= 1; x++) {
            for (int y = -1; y <= 1; y++) {
                for (int z = -1; z <= 1; z++) {
                    ivec3 neighborCellPos = cellPos + ivec3(x, y, z);
                    
                    if (any(lessThan(neighborCellPos, ivec3(0))) || 
                        any(greaterThanEqual(neighborCellPos, gridDim)))
                        continue;
                        
                    uint neighborCellIndex = getCellIndex(pos + vec3(x,y,z) * cellSize);
                    uint particlesInCell = cellCounts[neighborCellIndex];
                    
                    for (uint j = 0; j < particlesInCell && j < maxParticlesPerCell; j++) {
                        uint neighborId = cellParticles[neighborCellIndex * maxParticlesPerCell + j];
                        
                        if (neighborId == id) continue;
                        
                        vec3 neighborPos = particles[neighborId].position;
                        vec3 r = pos - neighborPos;
                        float rlen = length(r);
                        
                        if (rlen < h && rlen > 0.0001) {
                            vec3 gradW = gradW_Vorticity(r, rlen, h);
                            float falloff = max(0.0, 1.0 - rlen/h);
                            float estimatedVortMag = vorticityMagnitude * falloff;
                            
                            eta += gradW * (vorticityMagnitude - estimatedVortMag);
                        }
                    }
                }
            }
        }
        
        float etaLength = length(eta);
        if (etaLength > 0.0001) {
            vec3 N = normalize(eta);
            float amplifiedEpsilon = vorticityEpsilon * 10.0;
            vorticityForce = amplifiedEpsilon * cross(N, vorticity);
        }
    }
    
    
    //Apply forces
    particles[id].velocity += vorticityForce * dt;
    particles[id].velocity += xsphViscosityCoeff * xsphVelocityChange;
}