#version 430 core

layout(local_size_x = 256) in;

struct Particle {
    vec3 position;
    float padding1;
    vec3 velocity;
    float padding2;
    vec3 predictedPos;
    float padding3;
    vec3 color;
    float padding4;
    float density;
    float lambda;
    vec2 padding5;
};

layout(std140, binding = 0) uniform SimulationParams {
    float dt;
    float _pad0;
    float _pad1;
    float _pad2;

    vec4 gravity;

    float particleRadius;
    float h;
    float _pad3;
    float _pad4;

    vec4 minBoundary;
    vec4 maxBoundary;

    uint numParticles;
    float cellSize;
    uint maxParticlesPerCell;
    float restDensity;

    float vorticityEpsilon;
    float xsphViscosityCoeff;
    float _pad5;
    float _pad6; 
};

layout(std430, binding = 1) buffer ParticleBuffer {
    Particle particles[];
};

layout(std430, binding = 2) buffer CellCounts {
    uint cellCounts[];
};

layout(std430, binding = 3) buffer CellParticles {
    uint cellParticles[];
};

vec3 gradW_Spiky(vec3 r, float rlen, float h) {
    if(rlen > h || rlen < 0.0001) return vec3(0.0);
    
    float h2 = h*h;
    float h6 = h2*h2*h2;
    
    float factor = -45.0/(3.14159 * h6) * pow(h - rlen, 2);
    return factor * r / max(rlen, 0.0001);
}

float W_Poly6(float r, float h) {
    if(r > h) return 0.0;
    float term = h*h - r*r;
    return 315.0f / (64.0f * 3.14159f * pow(h, 9)) * term * term * term;
}

uint getCellIndex(vec3 position) {
    ivec3 cellPos = ivec3(floor((position - minBoundary.xyz) / cellSize));
    ivec3 gridDim = ivec3(ceil((maxBoundary.xyz - minBoundary.xyz) / cellSize));
    cellPos = clamp(cellPos, ivec3(0), gridDim - ivec3(1));
    return uint(cellPos.x + cellPos.y * gridDim.x + cellPos.z * gridDim.x * gridDim.y);
}

vec3 calculateWallRepulsion(vec3 pos) {
    vec3 repulsion = vec3(0.0);
    float wallRepulsionStrength = 1.0;
    float maxInfluenceDistance = 1.5 * particleRadius;
    
    float distToFloor = pos.y - (minBoundary.y + particleRadius);
    if (distToFloor < maxInfluenceDistance) {
        float repulsionForce = (1.0 - distToFloor/maxInfluenceDistance) * wallRepulsionStrength;
        repulsion.y += repulsionForce;
    }
    float distToLeftWall = pos.x - (minBoundary.x + particleRadius);
    if (distToLeftWall < maxInfluenceDistance) {
        float repulsionForce = (1.0 - distToLeftWall/maxInfluenceDistance) * wallRepulsionStrength;
        repulsion.x += repulsionForce;
    }
    
    float distToRightWall = (maxBoundary.x - particleRadius) - pos.x;
    if (distToRightWall < maxInfluenceDistance) {
        float repulsionForce = (1.0 - distToRightWall/maxInfluenceDistance) * wallRepulsionStrength;
        repulsion.x -= repulsionForce;
    }
    
    float distToFrontWall = pos.z - (minBoundary.z + particleRadius);
    if (distToFrontWall < maxInfluenceDistance) {
        float repulsionForce = (1.0 - distToFrontWall/maxInfluenceDistance) * wallRepulsionStrength;
        repulsion.z += repulsionForce;
    }
    
    float distToBackWall = (maxBoundary.z - particleRadius) - pos.z;
    if (distToBackWall < maxInfluenceDistance) {
        float repulsionForce = (1.0 - distToBackWall/maxInfluenceDistance) * wallRepulsionStrength;
        repulsion.z -= repulsionForce;
    }
    
    return repulsion;
}

void main() {
    uint id = gl_GlobalInvocationID.x;
    if (id >= numParticles) return;
    
    vec3 pos = particles[id].predictedPos;
    
    ivec3 cellPos = ivec3(floor((pos - minBoundary.xyz) / cellSize));
    ivec3 gridDim = ivec3(ceil((maxBoundary.xyz - minBoundary.xyz) / cellSize));
    
    vec3 deltaPos = vec3(0.0);
    
    for (int x = -1; x <= 1; x++) {
        for (int y = -1; y <= 1; y++) {
            for (int z = -1; z <= 1; z++) {
                ivec3 neighborCellPos = cellPos + ivec3(x, y, z);
                
                if (any(lessThan(neighborCellPos, ivec3(0))) || 
                    any(greaterThanEqual(neighborCellPos, gridDim)))
                    continue;
                    
                uint neighborCellIndex = getCellIndex(pos + vec3(x,y,z) * cellSize);
                uint particlesInCell = cellCounts[neighborCellIndex];
                
                for (uint j = 0; j < particlesInCell && j < maxParticlesPerCell; j++) {
                    uint neighborId = cellParticles[neighborCellIndex * maxParticlesPerCell + j];
                    
                    if (neighborId == id) continue;
                    
                    vec3 neighborPos = particles[neighborId].predictedPos;
                    vec3 diff = pos - neighborPos;
                    float dist = length(diff);
                    
                    if (dist < h && dist > 0.0001) {
                        vec3 gradW = gradW_Spiky(diff, dist, h);
                        float lambdaSum = particles[id].lambda + particles[neighborId].lambda;
                        
                        //s_corr equation 13
                        float k = 0.1;
                        float n = 4.0;     
                        float deltaq = 0.2 * h;
                        
                        float wij = W_Poly6(dist, h);
                        float wdeltaq = W_Poly6(deltaq, h);
                        
                        if (wdeltaq > 0.0) {
                            float scorr = -k * pow(wij / wdeltaq, n);
                            deltaPos += (lambdaSum + scorr) * gradW;
                        }
                    }
                }
            }
        }
    }
    
    
    deltaPos /= restDensity;
    
    vec3 wallRepulsion = calculateWallRepulsion(pos);
    deltaPos += wallRepulsion * 0.010;
    
    particles[id].predictedPos += deltaPos;
    float safetyMargin = 0.1 * particleRadius;
    

    if (particles[id].predictedPos.y < minBoundary.y + particleRadius) {
        particles[id].predictedPos.y = minBoundary.y + particleRadius + safetyMargin;
    }
    
    if (particles[id].predictedPos.x < minBoundary.x + particleRadius) {
        particles[id].predictedPos.x = minBoundary.x + particleRadius + safetyMargin;
    }
    if (particles[id].predictedPos.x > maxBoundary.x - particleRadius) {
        particles[id].predictedPos.x = maxBoundary.x - particleRadius - safetyMargin;
    }
    if (particles[id].predictedPos.z < minBoundary.z + particleRadius) {
        particles[id].predictedPos.z = minBoundary.z + particleRadius + safetyMargin;
    }
    if (particles[id].predictedPos.z > maxBoundary.z - particleRadius) {
        particles[id].predictedPos.z = maxBoundary.z - particleRadius - safetyMargin;
    }
}