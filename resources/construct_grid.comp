#version 430 core

layout(local_size_x = 256) in;

struct Particle {
    vec3 position;
    float padding1;
    vec3 velocity;
    float padding2;
    vec3 predictedPos;
    float padding3;
    vec3 color;
    float padding4;
    float density;
    float lambda;
    vec2 padding5;
};

layout(std140, binding = 0) uniform SimulationParams {
    float dt;
    float _pad0;
    float _pad1;
    float _pad2;

    vec4  gravity;

    float particleRadius;
    float h;
    float _pad3;
    float _pad4;

    vec4 minBoundary;
    vec4 maxBoundary;

    uint numParticles;
    float cellSize;
    uint maxParticlesPerCell;
    float restDensity;

    float vorticityEpsilon;
    float xsphViscosityCoeff;
    float _pad5;
    float _pad6; 
};

layout(std430, binding = 1) buffer ParticleBuffer {
    Particle particles[];
};

//tracks how many particles are in each cell
layout(std430, binding = 2) buffer CellCounts {
    uint cellCounts[];
};

//particle indices stored in each cell
layout(std430, binding = 3) buffer CellParticles {
    uint cellParticles[];
};


uint getCellIndex(vec3 position) {
    //grid cell coordinates
    ivec3 cellPos = ivec3(floor((position - minBoundary.xyz) / cellSize));
    
    //grid dimensions
    ivec3 gridDim = ivec3(ceil((maxBoundary.xyz - minBoundary.xyz) / cellSize));
  
    cellPos = clamp(cellPos, ivec3(0), gridDim - ivec3(1));
    
    //1D index
    return uint(cellPos.x + cellPos.y * gridDim.x + cellPos.z * gridDim.x * gridDim.y);
}

void main() {
    uint id = gl_GlobalInvocationID.x;
    if (id >= numParticles) return;
   
    memoryBarrierBuffer();
    barrier();
    
    uint cellIdx = getCellIndex(particles[id].predictedPos);
    
    uint insertIndex = atomicAdd(cellCounts[cellIdx], 1);
    if (insertIndex < maxParticlesPerCell) {
        cellParticles[cellIdx * maxParticlesPerCell + insertIndex] = id;
    }
}